load("@bazel_tools//tools/build_defs/cc:action_names.bzl", "ACTION_NAMES")
load("@bazel_tools//tools/cpp:cc_toolchain_config_lib.bzl",
     "feature",
     "flag_group",
     "flag_set",
     "tool_path")

all_compile_actions = [
    ACTION_NAMES.c_compile,
    ACTION_NAMES.cpp_compile,
    ACTION_NAMES.linkstamp_compile,
    ACTION_NAMES.assemble,
    ACTION_NAMES.preprocess_assemble,
    ACTION_NAMES.cpp_header_parsing,
    ACTION_NAMES.cpp_module_compile,
    ACTION_NAMES.cpp_module_codegen,
    ACTION_NAMES.clif_match,
    ACTION_NAMES.lto_backend,
]

all_link_actions = [
    ACTION_NAMES.cpp_link_executable,
    ACTION_NAMES.cpp_link_dynamic_library,
    ACTION_NAMES.cpp_link_nodeps_dynamic_library,
]

def _impl(ctx):
    tool_paths = [
        tool_path(
            name = "gcc",
            path = "bin/%{target}-gcc",
        ),
        tool_path(
            name = "ld",
            path = "bin/%{target}-ld",
        ),
        tool_path(
            name = "cpp",
            path = "bin/%{target}-g++",
        ),
        tool_path(
            name = "gcov",
            path = "bin/%{target}-gcov",
        ),
        tool_path(
            name = "nm",
            path = "bin/%{target}-nm",
        ),
        tool_path(
            name = "objdump",
            path = "bin/%{target}-objdump",
        ),
        tool_path(
            name = "strip",
            path = "bin/%{target}-strip",
        ),
        tool_path(
            name = "ar",
            path = "bin/%{target}-ar",
        ),
    ]

    opt_feature = feature(
        name = "opt",
        flag_sets = [
            flag_set(
                actions = all_compile_actions,
                flag_groups = ([
                    flag_group(
                        flags = [
                            "-O3",
                        ]
                    ),
                ]),
            ),
        ],
    )
    fastbuild_feature = feature(name = "fastbuild")
    dbg_feature = feature(
        name = "dbg",
        flag_sets = [
            flag_set(
                actions = all_compile_actions,
                flag_groups = ([
                    flag_group(
                        flags = [
                            "-g3",
                        ]
                    ),
                ]),
            ),
        ],
    )

    supports_pic_feature = feature(name = "supports_pic", enabled = True)
    supports_dynamic_linker_feature = feature(name = "supports_dynamic_linker", enabled = False)

    default_compile_flags = feature(
        name = "default_compile_flags",
        enabled = True,
        flag_sets = [
            flag_set(
                actions = all_compile_actions,
                flag_groups = ([
                    flag_group(
                        flags = [
                            "-g1",
                            "-Wall",
                            "-I%{repo_path}/%{target}/include/c++/6.5.0",
                        ],
                    ),
                ]),
            ),
        ],
    )

    linker_flags = ["-lstdc++"]
    if "%{target}" == "x86_64-unknown-linux-gnu":
        linker_flags.append("-lrt")

    default_linker_flags = feature(
        name = "default_linker_flags",
        enabled = True,
        flag_sets = [
            flag_set(
                actions = all_link_actions,
                flag_groups = ([flag_group(flags = linker_flags)]),
            ),
        ],
    )

    features = [
        opt_feature,
        fastbuild_feature,
        dbg_feature,
        supports_pic_feature,
        supports_dynamic_linker_feature,
        default_compile_flags,
        default_linker_flags,
    ]

    return cc_common.create_cc_toolchain_config_info(
        ctx = ctx,
        features = features,
        toolchain_identifier = "%{target}-cross-toolchain",
        host_system_name = "%{target}",
        target_system_name = "%{target}",
        target_cpu = "%{target}",
        target_libc = "glibc-2.14",
        compiler = "clang",
        abi_version = "clang-10.0.0",
        abi_libc_version = "%{target}",
        tool_paths = tool_paths,
        cxx_builtin_include_directories = [
            "%sysroot%/usr/include",
            "%{repo_path}/%{target}/include/c++/6.5.0",
            "%{repo_path}/lib/gcc/%{target}/6.5.0/include",
            "%{repo_path}/lib/gcc/%{target}/6.5.0/include-fixed",
        ],
        builtin_sysroot = "%{repo_path}/%{target}/sysroot",
    )

cc_toolchain_config = rule(
    implementation = _impl,
    attrs = {},
    provides = [CcToolchainConfigInfo],
)
